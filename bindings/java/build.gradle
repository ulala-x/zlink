plugins {
    id 'java'
    id 'maven-publish'
}

group = 'dev.kairoscode'
version = '0.9.0'
description = 'zlink Java bindings â€” high-performance async messaging library'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.2'
}

test {
    useJUnitPlatform()
    jvmArgs '--enable-native-access=ALL-UNNAMED'
    filter {
        excludeTestsMatching 'io.ulalax.zlink.integration.*'
    }
}

tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    jvmArgs '--enable-native-access=ALL-UNNAMED'
    shouldRunAfter test
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    filter {
        includeTestsMatching 'io.ulalax.zlink.integration.*'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'zlink'
            version = project.version
        }
    }
    repositories {
        maven {
            name = 'releaseRepo'
            def repoUrl = System.getenv('MAVEN_REPOSITORY_URL')
            if (repoUrl == null || repoUrl.trim().isEmpty()) {
                def ghRepo = System.getenv('GITHUB_REPOSITORY')  // owner/repo
                if (ghRepo != null && !ghRepo.trim().isEmpty()) {
                    repoUrl = "https://maven.pkg.github.com/${ghRepo}"
                } else {
                    repoUrl = "${buildDir}/repo"
                }
            }
            url = uri(repoUrl)
            credentials {
                def u = System.getenv('MAVEN_REPOSITORY_USERNAME')
                def p = System.getenv('MAVEN_REPOSITORY_PASSWORD')
                if ((u == null || u.trim().isEmpty()) && (p == null || p.trim().isEmpty())) {
                    u = System.getenv('GITHUB_ACTOR')
                    p = System.getenv('GITHUB_TOKEN')
                }
                username = u
                password = p
            }
        }
    }
}
